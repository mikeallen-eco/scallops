
model{
  for(i in 1:nSites) {
    # biological process
    lpsi1[i] <- lpsiInt
    psi1[i] <- ilogit(lpsi1[i])
    z[i, 1] ~ dbern(psi1[i])
    for(t in 2:nYears) {
      lphi[i, t-1] <- lphiInt[t-1]
      phi[i, t-1] <-
        ilogit(lphi[i,t-1])
      lgam[i, t-1] <- lgamInt[t-1]
      gamma[i, t-1] <- 
        ilogit(lgam[i, t-1])
      z[i, t] ~ dbern(z[i, t-1]*phi[i, t-1] +
          (1 - z[i, t-1])*gamma[i, t-1])
   }
    # detection process
    for(t in 1:nYears) {
      y[i, t] ~ dbin(p[t] * z[i, t], n[i, t])
      log.lik[i,t]  <- logdensity.bin(y[i,t],p[t],n[i,t]) # for lppd
    }
  }

  # Priors
  psiInt ~ dbeta(1, 1)
  lpsiInt <- logit(psiInt)

  for(t in 1:(nYears-1)) {
    phiInt[t] ~ dbeta(1, 1)
    lphiInt[t] <- logit(phiInt[t])
    gamInt[t] ~ dbeta(1, 1)
    lgamInt[t] <- logit(gamInt[t])
  }

  for(t in 1:nYears) {
    p[t] ~ dbeta(1, 1)
  }

  # Derived variable
  for(t in 1:nYears) {
    N[t] <- sum(z[,t]) # no. sites occupied for each year
  }

}
