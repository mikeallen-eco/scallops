
model{
  for(i in 1:nSites) {
    # biological process
    lpsi1[i] <- lpsiInt + betapsiO2*O2a[i,1] + betapsistrat*strat[i,1]
    psi1[i] <- ilogit(lpsi1[i])*exp(-0.5*((climvar1[i,1]-Topt1)/width1)^2)
    z[i, 1] ~ dbern(psi1[i])
    for(t in 2:nYears) {
      lphi[i, t-1] <- lphiInt[t-1] + betaphiO2*O2b[i,t-1] +
                        betaphistrat*strat[i,t-1]
      phi[i, t-1] <-
        ilogit(lphi[i,t-1])*exp(-0.5*((climvar2[i,t-1]-Topt2)/width2)^2)
      lgam[i, t-1] <- lgamInt[t-1] + betagamO2*O2c[i,t-1] +
                        betagamstrat*strat[i,t-1]
      gamma[i, t-1] <- 
        ilogit(lgam[i, t-1])*exp(-0.5*((climvar3[i,t-1]-Topt3)/width3)^2)
      z[i, t] ~ dbern(z[i, t-1]*phi[i, t-1] +
          (1 - z[i, t-1])*gamma[i, t-1])
   }
    # detection process
    for(t in 1:nYears) {
      y[i, t] ~ dbin(p[t] * z[i, t], n[i, t])
    }
  }

  # Priors
  psiInt ~ dbeta(1, 1)
  lpsiInt <- logit(psiInt)

  for(t in 1:(nYears-1)) {
    phiInt[t] ~ dbeta(1, 1)
    lphiInt[t] <- logit(phiInt[t])
    gamInt[t] ~ dbeta(1, 1)
    lgamInt[t] <- logit(gamInt[t])
  }

  for(t in 1:nYears) {
    p[t] ~ dbeta(1, 1)
  }

  Topt1 ~ dnorm(8,16)
  width1 ~ dnorm(5,10)
  Topt2 ~ dnorm(8,16)
  width2 ~ dnorm(5,10)
  Topt3 ~ dnorm(8,16)
  width3 ~ dnorm(5,10)
  betapsiO2 ~ dnorm(0,5)
  betaphiO2 ~ dnorm(0,5)
  betagamO2 ~ dnorm(0,5)
  betapsistrat ~ dnorm(0,5)
  betaphistrat ~ dnorm(0,5)
  betagamstrat ~ dnorm(0,5)

  # Derived variable
  for(t in 1:nYears) {
    N[t] <- sum(z[,t]) # no. sites occupied for each year
  }
  
}
