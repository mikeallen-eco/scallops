
model{
  for(i in 1:nSites) {
    # biological process
    lpsi1[i] <- lpsiInt + bpsi*climvar[i, 1] + 
          b2psi*climvar2[i, 1]
    psi1[i] <- ilogit(lpsi1[i])
    z[i, 1] ~ dbern(psi1[i])
    for(t in 2:nYears) {
      lphi[i, t-1] <- lphiInt[t-1] + bphi*climvar[i, t-1] + 
          b2phi*climvar2[i, t-1]
      phi[i, t-1] <- ilogit(lphi[i, t-1])
      lgam[i, t-1] <- lgamInt[t-1] + bgam*climvar[i, t-1] + 
          b2gam*climvar2[i, t-1]
      gamma[i, t-1] <- ilogit(lgam[i, t-1])
      z[i, t] ~ dbern(z[i, t-1]*phi[i, t-1] +
          (1 - z[i, t-1])*gamma[i, t-1])
   }
    # detection process
    for(t in 1:nYears) {
      y[i, t] ~ dbin(p[t] * z[i, t], n[i, t])
    }
  }

  # Priors
  psiInt ~ dbeta(1, 1)
  lpsiInt <- logit(psiInt)

  for(t in 1:(nYears-1)) {
    phiInt[t] ~ dbeta(1, 1)
    lphiInt[t] <- logit(phiInt[t])
    gamInt[t] ~ dbeta(1, 1)
    lgamInt[t] <- logit(gamInt[t])
  }

  for(t in 1:nYears) {
    p[t] ~ dbeta(1, 1)
  }

  bpsi ~ dunif(-5,5)
  bphi ~ dunif(-5,5)
  bgam ~ dunif(-5,5)
  b2psi ~ dunif(-5,5)
  b2phi ~ dunif(-5,5)
  b2gam ~ dunif(-5,5)

  # Derived variable
  for(t in 1:nYears) {
    N[t] <- sum(z[,t]) # no. sites occupied for each year
  }
  
}
